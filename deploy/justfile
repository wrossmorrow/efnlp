
AWS_ACCOUNT_ID := `aws sts get-caller-identity | jq -r '.Account'`
ECR_REPOSITORY := "efnlp"

tf COMMAND="plan" *FLAGS="":
	terraform {{COMMAND}} {{FLAGS}}

ecr-login REGION="us-east-1":
	aws ecr get-login-password --region {{REGION}} \
		| docker login --username AWS \
			--password-stdin {{AWS_ACCOUNT_ID}}.dkr.ecr.{{REGION}}.amazonaws.com

build REGION="us-east-1" IMAGE_TAG=`git rev-parse HEAD`: 
	cd ../go \
		&& docker build . \
			-t {{AWS_ACCOUNT_ID}}.dkr.ecr.{{REGION}}.amazonaws.com/{{ECR_REPOSITORY}}:{{IMAGE_TAG}}

push REGION="us-east-1" IMAGE_TAG=`git rev-parse HEAD`: 
	docker push {{AWS_ACCOUNT_ID}}.dkr.ecr.{{REGION}}.amazonaws.com/{{ECR_REPOSITORY}}:{{IMAGE_TAG}}

release REGION="us-east-1" IMAGE_TAG=`git rev-parse HEAD`: build push
	docker tag \
		{{AWS_ACCOUNT_ID}}.dkr.ecr.{{REGION}}.amazonaws.com/{{ECR_REPOSITORY}}:{{IMAGE_TAG}} \
		{{AWS_ACCOUNT_ID}}.dkr.ecr.{{REGION}}.amazonaws.com/{{ECR_REPOSITORY}}:latest
	docker push {{AWS_ACCOUNT_ID}}.dkr.ecr.{{REGION}}.amazonaws.com/{{ECR_REPOSITORY}}:latest
